# --enable-vala-pack-plugin=auto/yes/no
AC_ARG_ENABLE([vala-pack-plugin],
              [AS_HELP_STRING([--enable-vala-pack-plugin=@<:@auto/yes/no@:>@],
                              [Build with support for vala enhancements such as auto completion.])],
              [enable_vala_pack_plugin=$enableval],
              [enable_vala_pack_plugin=auto])

AS_IF([test x$enable_vala_pack_plugin != xno],[
	PKG_CHECK_MODULES(VALA, [libvala-0.30 >= 0.29.3],[enable_vala_pack_plugin=yes],[
		AS_IF([test x$enable_vala_pack_plugin = xyes],[
			AC_MSG_ERROR([Failed to locate vala. Please install the vala-devel package.])
		])
		enable_vala_pack_plugin=no
	])
])

# for if ENABLE_VALA_PACK_PLUGIN in Makefile.am
AM_CONDITIONAL(ENABLE_VALA_PACK_PLUGIN, test x$enable_vala_pack_plugin != xno)

# Ensure our makefile is generated by autoconf
AC_CONFIG_FILES([plugins/vala-pack/Makefile])
